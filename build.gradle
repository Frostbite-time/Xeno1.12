buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath('com.guardsquare:proguard-gradle:7.0.0') {
			exclude group: 'com.android.tools.build'
		}
    }
}

apply plugin: 'net.minecraftforge.gradle'

group = 'theic2'
version = '1.0-SNAPSHOT'
archivesBaseName = 'Xenobyte'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    runs {
        client {
            workingDirectory project.file('run/client')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                xenobyte {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run/server')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            mods {
                xenobyte {
                    source sourceSets.main
                }
            }
            args "nogui"
        }
    }
}

configurations {
    shade
    shadeOnly
    shadeOnly.extendsFrom shade
    compile.extendsFrom shade
}

repositories {
    mavenCentral()
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    compileOnly 'org.projectlombok:lombok:1.18.12'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
}

jar.manifest {
    attributes "Maven-Artifact": "${group}:${name}:${version}"
}
jar.finalizedBy('obfuscateProGuard')

def generateString(String alphabet, int n) {
    return new Random().with {
        (1..n).collect { alphabet[nextInt(alphabet.length())] }.join()
    }
}

def generateDictionaryFile(String name) {
    File dictionaryFile = file(name)
    if (dictionaryFile.exists())
        return;
    String outputData = ""
    for (int i = 0; i < 2000; i++)
        outputData += generateString((('a'..'z') + ('A'..'Z') + ('0'..'9')).join(), 16) + "\n"
    dictionaryFile.getParentFile().mkdirs()
    dictionaryFile.delete()
    dictionaryFile.write outputData
}

task generateDictionary {
    doFirst {
        generateDictionaryFile("$buildDir/proguard/class-dictionary.txt")
        generateDictionaryFile("$buildDir/proguard/package-dictionary.txt")
        generateDictionaryFile("$buildDir/proguard/fieldmethod-dictionary.txt")
    }
}

task obfuscateProGuard(type: proguard.gradle.ProGuardTask) {
    injars "$buildDir/libs/${project.name}-${project.version}.jar"
    outjars "$buildDir/libs/out-${project.name}-${project.version}.jar"
    libraryjars "${System.getProperty("java.home")}/lib/rt.jar"
    libraryjars "${System.getProperty("java.home")}/lib/jce.jar"
    libraryjars sourceSets.main.compileClasspath
    keepattributes "RuntimeVisibleAnnotations,RuntimeVisibleParameterAnnotations,RuntimeVisibleTypeAnnotations"
    classobfuscationdictionary "$buildDir/proguard/class-dictionary.txt"
    packageobfuscationdictionary "$buildDir/proguard/package-dictionary.txt"
    obfuscationdictionary "$buildDir/proguard/fieldmethod-dictionary.txt"
    dontshrink
    dontoptimize
    repackageclasses generateString((('a'..'z') + ('A'..'Z') + ('0'..'9')).join(), 16)
    dontwarn
    dontnote
    ignorewarnings
    printmapping "$buildDir/libs/proguard-${project.name}-${project.version}.map"
    keep "class internal.** { *; }"
    doLast {
        File from = file("$buildDir/libs/out-${project.name}-${project.version}.jar")
        File to = file("$buildDir/libs/${project.name}-${project.version}.jar")
        String mappingsText = file("$buildDir/libs/proguard-${project.name}-${project.version}.map").text
        java.util.regex.Pattern p = java.util.regex.Pattern.compile("theic2\\.xenobyteport\\.api\\.integration\\.JavaInjector -> (.*?):")
        java.util.regex.Matcher m = p.matcher(mappingsText)
        m.find();
        theic2.Commenter.addComment(from.toPath(), to.toPath(), m.group(1));
    }
}

obfuscateProGuard.dependsOn generateDictionary
obfuscateProGuard.finalizedBy('reobfJar')

jar {
    configurations.shadeOnly.each { dep ->
        from(dep.isDirectory() ? dep : project.zipTree(dep)) {
            rename 'LICENSE.txt', 'org/spongepowered/LICENSE.txt'
            exclude 'META-INF/*.SF'
            exclude 'META-INF/*.RSA'
            exclude 'META-INF/*.MF'
            exclude '**/*.html'
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    options.incremental = true // one flag, and things will get MUCH faster
}

defaultTasks 'build'
